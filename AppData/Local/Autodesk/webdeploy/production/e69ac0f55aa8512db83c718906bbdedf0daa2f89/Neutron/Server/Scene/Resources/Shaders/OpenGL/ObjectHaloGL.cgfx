//**************************************************************************/
// Copyright 2009 Autodesk, Inc.
// All rights reserved.
// 
// These coded instructions, statements, and computer programs contain
// unpublished proprietary information written by Autodesk, Inc., and are
// protected by Federal copyright law. They may not be disclosed to third
// parties or copied or duplicated in any form, in whole or in part, without
// the prior written consent of Autodesk, Inc.
//**************************************************************************/
// DESCRIPTION: Object halo detection.
//	  Creates a halo only on the outside of the object.
//    Takes an object id map and a object depth map as input.
//    To calculate the halo you check object ids and object depths for samples in a radius around 
//    the center pixel and compare them with the center pixels id and depth.
//    If you find a sample with different object id and that is 
//	  closer(smaller depth) than the center pixel return white as halo, else return black.
//
//    Inputs:  object ID map and a depth map. 
//    Output:  black and white texture where white stands for halo.
//
// AUTHOR: Charlotta Wadman
// CREATED: February 2010
//**************************************************************************/

// World-view-projection transformation.
float4x4 gWVPXf : WorldViewProjection < 
string UIWidget = "None"; >;

float2 gFinalres : ViewportPixelSize;

int gHaloWidth = 2;

//For some unknown reason, on Mac Intel HD 3000 graphics card, if we get HaloWidth from the variable "gHaloWidth" above, the system(OS) will hang
//Since in Neutron the default HaloWidth is 2, to work around the problem, we just hardcode the HaloWidth to 2 here
#ifndef HALO_WIDTH
#define HALO_WIDTH 2
#endif

float4 gScissorBox = float4(0,0,1,1);

// Filter input sampler.
texture2D gObjectID;

sampler2D ObjectIDSampler = sampler_state
{
    Texture = (gObjectID);
};

texture2D gDepth;

sampler2D DepthSampler = sampler_state
{
    Texture = (gDepth);
};

bool gUseHaloColor = false;

// Vertex shader input structure.
struct VS_INPUT
{
    float4 Pos : POSITION;
    float3 UV : TEXCOORD0;
};

// Vertex shader output structure.
struct VS_TO_PS
{
    float4 HPos : POSITION;
    float3 UV : TEXCOORD0;
};


// Vertex shader.
VS_TO_PS VS_NPR(VS_INPUT In)
{
    VS_TO_PS Out;

    // Transform the position from object space to clip space for output.
    Out.HPos = mul(gWVPXf, In.Pos);

    // Pass the texture coordinates unchanged.
    Out.UV = In.UV;

    return Out;
}

vec4 CalcHalo(float4 id, float idz, float4 center, float centerz, vec4 haloColorThis, float4 haloColor, inout float4 outColor)
{
    // if center pixel and sample have different id	
    if(any(center-id))
    {
        if(centerz < 0.99 && idz < 0.99)
		{
			if(gUseHaloColor)
				outColor = haloColorThis;
			else
				outColor = float4(1,1,1,1);
		}
		else if(centerz>idz )
		{
			if(gUseHaloColor)
				outColor = haloColor;
			else
				outColor = float4(1,1,1,1);
		}
    }

    return outColor;
}

float4 PS_NPR(VS_TO_PS In) : COLOR0
{
	float4 outColor = float4(0,0,0,0);
    float2 texCoord = In.UV.xy;
	int stepRadius = HALO_WIDTH;//Hardcode here to work around the problem on Intel HD 3000 graphics card
    float checkRadius = stepRadius +0.5;

    float2 offset;
    float2 sampleCoord;
    float4 sample, center;
    float samplez, centerz;

    float2 edgeOffset = float2(1/gFinalres.x,1/gFinalres.y);

    // retrieve the center and its z-depth, 
    center = tex2D(ObjectIDSampler, texCoord);
    float4 depthValue = tex2D(DepthSampler, texCoord);
    centerz = depthValue.r;
	float4 haloColorThis = float4(depthValue.gba,1);

	float epsilon = max(1/gFinalres.x,1/gFinalres.y);

	if(texCoord.x >= (gScissorBox.x - checkRadius * epsilon)
	&& texCoord.x <= (gScissorBox.x + gScissorBox.z + checkRadius * epsilon)
	&& texCoord.y >= (gScissorBox.y - checkRadius * epsilon)
	&& texCoord.y <= (gScissorBox.y + gScissorBox.w + checkRadius * epsilon) )
	{
		// loop through all points in a square around the center point
		for ( int x = -stepRadius; x <= stepRadius; x++ )
		{
			offset.x = float(x);
			for ( int y = -stepRadius; y <= stepRadius; y++ )
			{
				offset.y = float(y);

				// is the distance to this point inside the radius?
				if (length(offset)< checkRadius )
				{	
					sampleCoord.xy = texCoord.xy + edgeOffset.xy * offset.xy;

					sample  = tex2D(ObjectIDSampler, sampleCoord);
					float4 depthSample = tex2D(DepthSampler,sampleCoord);
					samplez = depthSample.r;
					float4 haloColor = float4(depthSample.g,depthSample.b,depthSample.a,1);

					outColor = CalcHalo( sample, samplez, center, centerz, haloColorThis, haloColor, outColor);
				}
			}
		}
	}
    return outColor;
}

technique Main
{
    pass p0
    {
        VertexProgram = compile glslv VS_NPR();
        FragmentProgram = compile glslf PS_NPR();

    }
}
